controller ExampleCtrl where

-- Inputs ----------------------------------------------------------------------

-- Play inputs
input p_0_Loiter_0_0 (P_0_Loiter_0_0) : Bool = False
input p_1_Loiter_0_0 (P_1_Loiter_0_0) : Bool = False
input p_1_ST_0_0     (P_1_ST_0_0)     : Bool = False
input p_1_ST_0_1     (P_1_ST_0_1)     : Bool = False

-- mutual exclusion of plays
--
-- NOTE: vehicle 0 only has one possible action: Loiter
env_trans
  mutex { p_1_Loiter_0_0, p_1_ST_0_0, p_1_ST_0_1 }

-- Monitor inputs
input m_0_Fuel_0_0  (M_0_Fuel_0_0)    : Bool = False
input m_1_Found_0_0 (M_1_Found_0_0)   : Bool = False
input m_1_Fuel_0_0  (M_1_Fuel_0_0)    : Bool = False

-- Outputs ---------------------------------------------------------------------

-- Behavior outputs
output b_0_Loiter_0_0 (B_0_Loiter_0_0) : Bool = False
output b_0_Refuel_0_0 (B_0_Refuel_0_0) : Bool = False

-- mutual exclusion for behaviors
sys_trans
  mutex { b_0_Loiter_0_0
        , b_0_Refuel_0_0
        }

output b_1_Refuel_0_0 (B_1_Refuel_0_0) : Bool = False
output b_1_Loiter_0_0 (B_1_Loiter_0_0) : Bool = False
output b_1_Search_1_0 (B_1_Search_1_0) : Bool = False
output b_1_Search_0_0 (B_1_Search_0_0) : Bool = False
output b_1_Track_0_0  (B_1_Track_0_0)  : Bool = False
output b_1_Track_1_0  (B_1_Track_1_0)  : Bool = False

-- mutual exclusion for behaviors
sys_trans
  mutex { b_1_Loiter_0_0
        , b_1_Search_0_0, b_1_Search_1_0
        , b_1_Track_0_0,  b_1_Track_1_0
        , b_1_Refuel_0_0
        }

-- Plays -----------------------------------------------------------------------

-- loitering
sys_trans
  (p_0_Loiter_0_0 /\ !m_0_Fuel_0_0) -> b_0_Loiter_0_0
  (p_1_Loiter_0_0 /\ !m_1_Fuel_0_0) -> b_1_Loiter_0_0

-- search/track
sys_trans
  (p_1_ST_0_0 /\ ! m_1_Fuel_0_0 /\ !m_1_Found_0_0) -> b_1_Search_0_0
  (p_1_ST_0_0 /\ ! m_1_Fuel_0_0 /\  m_1_Found_0_0) -> b_1_Track_0_0

  (p_1_ST_0_1 /\ ! m_1_Fuel_0_0 /\ !m_1_Found_0_0) -> b_1_Search_1_0
  (p_1_ST_0_1 /\ ! m_1_Fuel_0_0 /\  m_1_Found_0_0) -> b_1_Track_0_0

-- contingency
sys_trans
  m_0_Fuel_0_0 -> b_0_Refuel_0_0
  m_1_Fuel_0_0 -> b_1_Refuel_0_0
